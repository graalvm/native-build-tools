plugins {
    id 'java-library'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
    id 'org.graalvm.build.interop.publishing'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.graalvm.nativeimage', name: 'svm', version: org_graalvm_version
    implementation group: 'org.junit.platform', name: 'junit-platform-console', version: junit_platform_version
    implementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junit_platform_version
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junit_jupiter_version
    testImplementation(platform('org.junit:junit-bom:' + junit_jupiter_version))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.junit.vintage:junit-vintage-engine')
}

apply from: "gradle/native-image-testing.gradle"

clean.doLast {
    delete "${buildDir}/*"
}

test {
    useJUnitPlatform()
}

tasks.withType(Test) {
    testLogging {
        events "standardOut", "passed", "skipped", "failed"
    }
}

group = 'org.graalvm.buildtools'
version = junit_platform_native_version

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = rootProject.name
                description = 'JUnit Platform support for GraalVM Native Image'
                url = 'https://github.com/graalvm/native-build-tools'

                licenses {
                    license {
                        name = 'Universal Permissive License Version 1.0'
                        url = 'http://oss.oracle.com/licenses/upl'
                    }
                }

                developers {
                    developer {
                        name = 'SubstrateVM developers'
                        email = 'graal-dev@openjdk.java.net'
                        organization = 'Graal'
                        organizationUrl = 'http://openjdk.java.net/projects/graal'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/graalvm/native-build-tools.git'
                    developerConnection = 'scm:git:ssh://github.com:graalvm/native-build-tools.git'
                    url = 'https://github.com/graalvm/native-build-tools/tree/master'
                }
            }
            from components.java
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
